/*!*****************************************************************************
@brief
    Calculate a visibility triangle fan within the current scene, then draw it as a light field.
@date       07/11/2015
@author     Johannes Peter (j.peter\@digipen.edu)
@par        url: https://github.com/JohannesMP/Zilch-Snippets-and-Libraries
@details
    This process wass heavily inspired by the methods described in these two tutorials:
    - http://ncase.me/sight-and-light/
    - http://www.redblobgames.com/articles/visibility/

    Effectively the process is:
    1. Get a set of all relevant collider vertices (see 'ColliderPoints' component)
    2. Cast two Rays from the light source to the vertices (+/- very small angle).
    3. Store the results in an array of points.
    4. Sort the points array by angle, relative to the light.
    5. Loop over the ordered points, drawing a triangle from the light, the i'th point and the i+1'th point.

@copyright
    All content Â© 2015 DigiPen (USA) Corporation, all rights reserved
*******************************************************************************/

class LightCaster : ZilchComponent
{
    var LightPosition : Real3;
    
    var ShadowObjects : Array[Cog] = Array[Cog]();
    var ShadowPoints : Array[Real3] = Array[Real3]();
    var Line : DebugLine = DebugLine();
    var Triangle : DebugTriangle = DebugTriangle();
    var Point : DebugSphere = DebugSphere();
    
    var ShowPoints  : Boolean = false;
    var ShowColors  : Boolean = false;
    var ShowBorders : Boolean = false;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Space, Events.FrameUpdate, this.OnFrameUpdate);
        Zero.Connect(Zero.Keyboard, Events.KeyUp, this.OnKeyUp);
        this.Line.HeadSize(0.2);
        this.Line.Width(0.1);
        this.Line.Float4Color(Real4(0,1,0,0.5));
        this.Point.Radius(0.1);
        this.Point.Width(0.1);
        this.Point.Float4Color(Real4(1,0,0,1));
    }

    function OnKeyUp(event : KeyboardEvent)
    {
        if(event.Key == Keys.Two)
        {
            this.ShowPoints = !this.ShowPoints;
        }
        
        else if(event.Key == Keys.Three)
        {
            this.ShowColors = !this.ShowColors;
        }
        
        else if(event.Key == Keys.One)
        {
            this.ShowBorders = !this.ShowBorders;
        }
    }

    function OnFrameUpdate(event : UpdateEvent)
    {
        this.Triangle.Border(this.ShowBorders);
        this.Triangle.Position0(this.LightPosition);

        var i : Integer = 0;
        
        if(this.ShadowPoints.Count > 1)
        {
            for(i = 0; i < this.ShadowPoints.Count-1; ++i)
            {
                
                var factor = (i as Real)/this.ShadowPoints.Count;
                this.Triangle.Position1(this.ShadowPoints[i]);
                this.Triangle.Position2(this.ShadowPoints[i+1]);
                
                if(this.ShowColors)
                {
                    this.Triangle.Float4Color(Real4(1-factor, factor,1,0.5));
                }
                else
                {
                    this.Triangle.Float4Color(Real4(1,1,1,0.5));
                }
                this.Triangle.Draw();
                
            }
            // draw the last one
            this.Triangle.Position1(this.ShadowPoints[this.ShadowPoints.Count-1]);
            this.Triangle.Position2(this.ShadowPoints[0]);
            this.Triangle.Draw();
        }
        
        if(this.ShowPoints)
        {
            var object = this.ShadowObjects[this.ShadowObjects.Count-1];
            var points = ColliderPoints.GetColliderPoints(object, this.LightPosition);
            
            if(points!= null)
            {
                this.Line.Start(this.LightPosition);
                foreach(var point in points)
                {
                    this.Line.End(point);
                    this.Line.Draw();
                    this.Point.Position(point);
                    this.Point.Draw();
                }
            }
        }
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        this.LightPosition = this.GetMousePos();
        
        this.ShadowObjects.Clear();
        
        // grab each object
        foreach(var child in this.Space.FindObjectByName("ShadowCasters").Children)
        {
            
            if(child.ShadowCaster != null)
            {
                this.ShadowObjects.Add(child);
            }
        }
        
        // sort objects based on distance from source (used for debug)
        this.ShadowObjects.Sort(this.CompareCogDistances);
        
        
        // raycast each object to get light points
        var ray : Ray = Ray();
        ray.Start = this.LightPosition;
        this.ShadowPoints.Clear();
        foreach(var object in this.ShadowObjects)
        {
            var points = ColliderPoints.GetColliderPoints(object, this.LightPosition);
            if(points != null)
            {
                foreach (var point in points)
                {
                    var direction = point-ray.Start;
                    
                    // up
                    var up = Math.RotateVector(direction, Real3(0,0,1), 0.00001);
                    ray.Direction = up;
                    
                    var cast = this.Space.PhysicsSpace.CastRayFirst(ray);
                    if(cast.ObjectHit != null)
                    {
                        this.ShadowPoints.Add(cast.WorldPosition);
                    }
                    
                    // down
                    var down = Math.RotateVector(direction, Real3(0,0,1), -0.00001);
                    ray.Direction = down;
                    cast = this.Space.PhysicsSpace.CastRayFirst(ray);
                    if(cast.ObjectHit != null)
                    {
                        this.ShadowPoints.Add(cast.WorldPosition);
                    }
                }
            }
        }
        
        // sort light points by angle to source
        this.ShadowPoints.Sort(this.ComparePointAngles);
        
    }
    
    
    function ComparePointAngles(left : Real3, right : Real3) : Boolean
    {
        return this.GetPointSourceRotation(left) > this.GetPointSourceRotation(right);
    }
    
    function CompareCogDistances(left : Cog, right : Cog) : Boolean
    {
        return this.GetCogSourceDistanceSQ(left) > this.GetCogSourceDistanceSQ(right);
    }
    
    
    function GetPointSourceRotation(point : Real3) : Real
    {
        var diff = point - this.LightPosition;
        return Math.ATan2(diff.X, diff.Y);
    }
    
    function GetCogSourceDistanceSQ(cog : Cog) : Real
    {
        var diff = cog.Transform.WorldTranslation - this.LightPosition;
        return Math.LengthSq(diff);
    }
    
    
    function GetMousePos() : Real3
    {
        var mousePos = Zero.Mouse.ScreenPosition;
        var viewport = this.Space.LevelSettings.CameraViewport;
        return viewport.ScreenToWorldZPlane(mousePos, 0);
    }
}
